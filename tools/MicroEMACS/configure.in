dnl Autoconf configure script for MicroEMACS.
dnl Process this file with autoconf to produce a configure script.

AC_INIT
AC_CONFIG_SRCDIR([display.c])
AC_PROG_CC
AC_CANONICAL_TARGET
AC_CONFIG_HEADERS([config.h])
AC_CHECK_HEADERS(unistd.h)

CFLAGS_DEBUG="-g -DDEBUG=1"
CFLAGS_OPTIMIZE=-O

EXTRA_OBJS=""
case "${host}" in
*hpux*)		DEFINES="$DEFINES -DHPUX"; ;;
x86_64*linux*)	DEFINES="$DEFINES -DLINUX";
		LDFLAGS="-L/usr/lib64/termcap" ;;
*linux*)	DEFINES="$DEFINES -DLINUX" ;;
*solaris*)	DEFINES="$DEFINES -DSOLARIS"; EXTRA_OBJS=bcopy.o;
                bcopy=bcopy.s ;;
*cygwin)	DEFINES="$DEFINES -DCYGWIN" ;;
*mingw*)	DEFINES="$DEFINES -DMINGW" ;;
*)		DEFINES="$DEFINES -DUNKNOWN" ;;
esac
echo host is ${host}

dnl Set system and tty directories based on arguments.
AC_ARG_WITH(linux,
[  --with-linux            Use Linux-specific instead of generic UNIX I/O],
[sysdir=sys/linux],
[sysdir=sys/unix])

AC_ARG_WITH(termcap,
[  --with-termcap          Use termcap instead of ncurses for screen I/O],
[ttydir=tty/termcap
LIBS="-ltermcap"],
[ttydir=tty/ncurses
LIBS="-lncursesw"])

AC_ARG_WITH(ntconsole,
[  --with-ntconsole        Use NT-specific console I/O instead of generic UNIX I/O],
[ttydir=tty/nt])

AC_ARG_WITH(ruby,
[  --with-ruby             Add Ruby scripting support],
[EXTRA_OBJS="$EXTRA_OBJS ruby.o rubyapi.o"
CFLAGS_RUBY="`pkg-config --cflags ruby` -DUSE_RUBY=1 -DLIBRUBY=`pkg-config --variable=LIBRUBY_SO ruby`"
LIBS="$LIBS -ldl"],
[CFLAGS_RUBY=""
])

dnl Set host-specific system and tty directories
case "${host}" in
*mingw*)	ttydir=tty/mingw;
		sysdir=sys/mingw;
		LIBS="" ;;
esac

dnl we might have different ones depending on target platform.
# Set the default compiler switches based on the --enable-debug option

AC_ARG_ENABLE(debug,
[  --enable-debug          Build with debugging symbols],
[CFLAGS_DEFAULT=CFLAGS_DEBUG],
[CFLAGS_DEFAULT=CFLAGS_OPTIMIZE])

AC_SUBST(sysdir)
AC_SUBST(ttydir)
AC_SUBST(DEFINES)
AC_SUBST(EXTRA_OBJS)
AC_SUBST(LIBS)
AC_SUBST(CFLAGS_DEBUG)
AC_SUBST(CFLAGS_DEFAULT)
AC_SUBST(CFLAGS_OPTIMIZE)
AC_SUBST(CFLAGS_RUBY)

AC_CONFIG_FILES([Makefile])
AC_OUTPUT
